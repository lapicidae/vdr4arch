# This PKGBUILD is part of the VDR4Arch project [https://github.com/vdr4arch]

# Maintainer: Manuel Reimer <manuel.reimer@gmx.de>
pkgname=kodi-addon-pvr-hts
pkgver=21.2.6
_gitver=27fa10450aaffe9a469510f1dcc94ea0e0a5cdda
pkgrel=1
url="https://github.com/kodi-pvr/pvr.hts"
arch=('x86_64' 'i686' 'armv6h' 'armv7h' 'aarch64')
license=('GPL2')
pkgdesc="Kodi's Tvheadend HTSP client addon"
depends=('kodi')
makedepends=('cmake' 'git' 'kodi-dev' 'kodi-platform')
source=("git+https://github.com/kodi-pvr/pvr.hts.git#commit=$_gitver")
md5sums=('5688196333ae7c59e4e179e31e089705')

prepare() {
  cd "${srcdir}/pvr.hts/pvr.hts"
  git cherry-pick -n db34cca7cf02559a3438f3024eea38cf6d5b6674
}

pkgver() {
  cd "${srcdir}/pvr.hts/pvr.hts"
  grep '  version' addon.xml.in | cut -d'"' -f2
}

build() {
  mkdir -p "${srcdir}/pvr.hts/build"
  cd "${srcdir}/pvr.hts/build"

  cmake -DCMAKE_INSTALL_PREFIX="/usr" \
        -DCMAKE_INSTALL_LIBDIR=/usr/lib/kodi \
        -DCMAKE_BUILD_TYPE=Release \
        ..

  make
}

package() {
  cd "${srcdir}/pvr.hts/build"
  make DESTDIR="${pkgdir}" install
}
